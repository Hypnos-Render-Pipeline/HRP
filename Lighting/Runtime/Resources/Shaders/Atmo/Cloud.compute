#pragma kernel DownSampleDepth
#pragma kernel GetRayIndex
#pragma kernel MarchRay
#pragma kernel CheckboardUpsample
#pragma kernel BlitToHistory
#pragma kernel FullResolutionUpsample
#pragma kernel LoadVolumeData
#pragma kernel WriteCloudShadowMap
#pragma kernel BlurCloudShadowMap

float4 _Time;

#define T T_TAB
#include "../Includes/Atmo/CloudMarching.hlsl"

float4x4 _V_Inv, _VP_Inv_NoJitter, _Last_VP_NoJitter;

float4 _ZBufferParams;

float4 _WH;

Texture2D _MotionTex; SamplerState sampler_MotionTex;
Texture2D<float4> _SceneColorTex;

Texture2D<float> _Depth; SamplerState sampler_Depth;

RWTexture2D<float> _DownSampled_MinMax_Depth;

RWTexture2D<float2> _Ray_Index;

RWTexture2D<float4> _Marching_Result_A;
//RWTexture2D<float4> _Marching_Result_B;

Texture2D<float4> _History; SamplerState sampler_bilinear_clamp;

RWTexture2D<float4> _HalfResResult;

RWTexture2D<float4> _Cloud;

RWTexture2D<float4> _CloudSM;

inline float LinearEyeDepth(float z)
{
	return 1.0 / (_ZBufferParams.z * z + _ZBufferParams.w);
}

int offset2index(const uint2 offset) {
	int2x2 offset2index = int2x2(0, 3, 1, 2);
	return offset2index[offset.x][offset.y];
}
int2 index2offset(const uint index) {
	uint2 index2offset[4] = { uint2(0, 0), uint2(1, 0), uint2(1, 1), uint2(0, 1) };
	return index2offset[index];
}



[numthreads(8, 8, 1)]
void DownSampleDepth(const uint2 id : SV_DispatchThreadID)
{
	if (any(id >= uint2(_WH.xy / 2))) return;

	float2 uv = (id * 2 + 0.5) * _WH.zw;

	float4 d = _Depth.GatherRed(sampler_Depth, uv);

	int checkboard = (id.x % 2 + id.y) % 2;

	float min_d = min(d.x, min(d.y, min(d.z, d.w)));
	float max_d = max(d.x, max(d.y, max(d.z, d.w)));

	_DownSampled_MinMax_Depth[id] = LinearEyeDepth(checkboard ? min_d : max_d);
}



groupshared float picked_depth[10][10];

int get_picked_index_of_tile(const uint2 tile_id) {
	uint tile_checkboard = (tile_id.x % 2 + tile_id.y) % 2;
	return (tile_checkboard + _Clock) % 4;
}

float4 read_tile_from_downsampled_depth_tex(uint2 tile_id) {
	float2 uv = (tile_id * 2 + 0.5) * _WH.zw * 2;
	uv = saturate(uv);
	return _Depth.GatherRed(sampler_Depth, uv).wzyx;
}

float4 min_max_d_index(const float4 d) {
	float min_d = min(d.x, min(d.y, min(d.z, d.w)));
	float max_d = max(d.x, max(d.y, max(d.z, d.w)));

	float4 index_caster = float4(0, 1, 2, 3);

	return float4(min_d, max_d, dot(d == min_d, index_caster), dot(d == max_d, index_caster));
}

[numthreads(8, 8, 1)]
void GetRayIndex(const uint2 tile_id : SV_DispatchThreadID, const uint2 group_thread_id : SV_GroupThreadID, const uint2 group_id : SV_GroupID)
{
	uint flatten_group_thread_id = dot(group_thread_id, uint2(1, 8));
	int2 base_address = group_id * 8 - 1;

	// read data to shared
	{
		[loop]
		for (int i = 0; i < 2; i++)
		{
			flatten_group_thread_id = flatten_group_thread_id + i * 64;
			if (flatten_group_thread_id < 100) {
				int2 offset = uint2(flatten_group_thread_id % 10, flatten_group_thread_id / 10);
				int2 tile = base_address + offset;
				int index = get_picked_index_of_tile(tile);
				float4 d = read_tile_from_downsampled_depth_tex(tile);
				picked_depth[offset.x][offset.y] = d[index];
			}
		}
		GroupMemoryBarrierWithGroupSync();
	}

	int2 group_thread_id_plus_1 = group_thread_id + 1;
	float4 c_d = read_tile_from_downsampled_depth_tex(tile_id);
	float4 c_min_max_d_index = min_max_d_index(c_d);

	// refine index
	bool2 refine_flag = false;
	int2 nei[8] = { int2(-1, -1), int2(-1, 0), int2(-1, 1),
					int2(0, -1), int2(0, 1),
					int2(1, -1), int2(1, 0), int2(1, 1) };
	[unroll]
	for (int i = 0; i < 8; i++)
	{
		int2 sm_index = group_thread_id_plus_1 + nei[i];
		float nei_picked_depth = picked_depth[sm_index.x][sm_index.y];

		float2 normalized_bias = abs(c_min_max_d_index.xy - nei_picked_depth) / nei_picked_depth;

		refine_flag = refine_flag || (normalized_bias < 0.25);
	}
	int picked_index = refine_flag.x ? (refine_flag.y ? get_picked_index_of_tile(tile_id) : c_min_max_d_index.w) : c_min_max_d_index.z;

	float picked_d = c_d[picked_index];
	//if (picked_d > (1.0 / _ZBufferParams.w) - 0.5) picked_d = 1;
	/*else*/ picked_d /= 10000;

	if (any(tile_id >= uint2(_WH.xy / 4))) return;
	_Ray_Index[tile_id] = float2(picked_index, picked_d);
}

[numthreads(8, 8, 1)]
void MarchRay(const uint2 tile_id : SV_DispatchThreadID)
{
	if (any(tile_id >= uint2(_WH.xy / 4))) return;

	float2 index_depth = _Ray_Index[tile_id];
	int2 index_in_half_resolution = tile_id * 2 + index2offset(index_depth.x);

	float2 uv_in_half_resolution = (index_in_half_resolution + 0.5) * _WH.zw * 2;
	
	float3 wcpos = _V_Inv._m03_m13_m23;
	float3 p = wcpos;
	float3 pos;
	{
		float4 ndc = float4(uv_in_half_resolution * 2 - 1, 0, 1);
		float4 worldPos = mul(_VP_Inv_NoJitter, ndc);
		pos = worldPos.xyz / worldPos.w;
	}
	float3 v = normalize(pos - p);

	p.y = max(p.y, 95) + planet_radius;

	RandSeed(tile_id);
	float depth = index_depth.y * 10000;
	float dotvc = -dot(normalize(_V_Inv._m02_m12_m22), v);
	depth /= dotvc;
	float max_depth = (1.0 / _ZBufferParams.w) * 0.998;
	float far_clip_fade = smoothstep(0.9, 1, depth / max_depth);
	depth = lerp(depth, 999999, far_clip_fade);

	float cloud_dis;
	float av_occ;
	float4 res = CloudRender(p, v, cloud_dis, av_occ, depth);

	float3 cloud = 0;

	if (cloud_dis != 0) {
		// cloud shading
		float3 present_point = p + v * cloud_dis;
		float3 sunLight = Sunlight(present_point, _SunDir);
		float3 ambd = Tu_L(p, _SunDir) * _SunLuminance * 0.1;
		float3 sky = SkyBox(present_point, normalize(present_point), _SunDir) * _SunLuminance;
		float3 ambu = sky * (1 - res.z) * 3;
		float3 amb = (lerp(ambd, ambu, res.z * 0.7 + 0.3) + sunLight * 0.5 * res.z) * 4;
		cloud = (res.x * sunLight + res.y * amb) * _Brightness;

		// apply atmo fog to cloud
		float3 fog = SkyBox(p, v, _SunDir) * _SunLuminance * av_occ;
		float3 trans = T(p, present_point);
		trans = smoothstep(0.1, 0.97, trans);
		cloud = cloud * trans + fog * (1 - trans) * (1 - res.a);
	}

	_Marching_Result_A[tile_id] = float4(cloud, res.a);
}

groupshared float4 marching_Result[6][6];
groupshared float marching_Depth[6][6];

[numthreads(8, 8, 1)]
void CheckboardUpsample(const uint2 id : SV_DispatchThreadID, const uint2 group_thread_id : SV_GroupThreadID, const uint2 group_id : SV_GroupID)
{
	if (all(group_thread_id < 6)) {
		int2 responsed_tile_id = group_id * 4 - 1 + group_thread_id;
		responsed_tile_id = clamp(responsed_tile_id, 0, _WH.xy / 4 - 0.5);
		marching_Result[group_thread_id.x][group_thread_id.y] = _Marching_Result_A[responsed_tile_id];
		marching_Depth[group_thread_id.x][group_thread_id.y] = _Ray_Index[responsed_tile_id].y * 10000;
	}
	GroupMemoryBarrierWithGroupSync();

	uint2 tile_id = id / 2;
	uint2 groupshared_id = group_thread_id / 2 + 1;

	int2 index_in_half_resolution = id;
	float2 uv_in_half_resolution = (index_in_half_resolution + 0.5) * _WH.zw * 2;

	float d = _DownSampled_MinMax_Depth[id];


	float4 vpoint = float4(uv_in_half_resolution * 2 - 1, 0, 1);

	float4 wpoint;
	wpoint = mul(_VP_Inv_NoJitter, vpoint); wpoint /= wpoint.w;
	float4 lvpoint = mul(_Last_VP_NoJitter, wpoint);
	lvpoint /= lvpoint.w;
	lvpoint = (lvpoint + 1) * 0.5;
	float2 reproj_offset = uv_in_half_resolution - lvpoint.xy;
	float move_speed = length(reproj_offset);

	float4 history = _History.SampleLevel(sampler_bilinear_clamp, uv_in_half_resolution - reproj_offset, 0);

	int2 nei[9] = { int2(-1, -1), int2(-1, 0), int2(-1, 1),
					int2(0, -1), int2(0, 1),
					int2(1, -1), int2(1, 0), int2(1, 1) , int2(0,0) };
	float4 average_result = 0;
	float4 aabb_low = 100000, aabb_high = 0;
	float weight = 0;
	[unroll]
	for (int i = 0; i < 9; i++)
	{
		int2 nei_id = groupshared_id + nei[i];
		float depth = marching_Depth[nei_id.x][nei_id.y];
		if (abs(d - depth) / depth > 0.25) // if the depth difference is greater than 0.25, discard it
			continue;
		float4 result = marching_Result[nei_id.x][nei_id.y];
		aabb_low = min(aabb_low, result);
		aabb_high = max(aabb_high, result);
		average_result += result;
		weight += 1;
	}
	average_result /= weight;
	//_HalfResResult[id] = history; return;
	if (any(id >= uint2(_WH.xy / 2))) return;
	if (offset2index(id % 2) == int(_Ray_Index[id / 2].x)) {
		_HalfResResult[id] = lerp(clamp(history, aabb_low, aabb_high), marching_Result[groupshared_id.x][groupshared_id.y], lerp(0.5, 1, _Quality.w));
		return;
	}

	if (weight < 1) { // sometime sill lost sample, prevent NAN by this
		_HalfResResult[id] = history;
	}
	else {
		_HalfResResult[id] = clamp(history, aabb_low, aabb_high);
	}
}

[numthreads(8, 8, 1)]
void BlitToHistory(const uint2 id : SV_DispatchThreadID)
{
	if (any(id >= uint2(_WH.xy / 2))) return;
	_HalfResResult[id] = _History[id];
}


groupshared float4 half_Result[6][6];
groupshared float half_Depth[6][6];

[numthreads(8, 8, 1)]
void FullResolutionUpsample(const uint2 id : SV_DispatchThreadID, const uint2 group_thread_id : SV_GroupThreadID, const uint2 group_id : SV_GroupID)
{
	if (all(group_thread_id < 6)) {
		int2 responsed_tile_id = group_id * 4 - 1 + group_thread_id;
		responsed_tile_id = clamp(responsed_tile_id, 0, _WH.xy / 2 - 0.5);
		half_Result[group_thread_id.x][group_thread_id.y] = _History[responsed_tile_id];
		half_Depth[group_thread_id.x][group_thread_id.y] = _DownSampled_MinMax_Depth[responsed_tile_id];
	}
	GroupMemoryBarrierWithGroupSync();

	uint2 groupshared_id = group_thread_id / 2 + 1;
	float2 groupshared_uv = (group_thread_id + Roberts2_(id.x % 2 + id.y)) / 8.0f * 4.0f / 6.0f + 1.0f / 6.0f; // convert index to uv in group memory space

	float raw_d = _Depth[id];
	float d = LinearEyeDepth(raw_d);

	int2 lb_id = (id % 2) - 1 + groupshared_id;

	int2 offsets[4] = { int2(0,0), int2(0, 1),
						int2(1, 0), int2(1, 1),
		/*int2(-1, -1), int2(-1, 0), int2(-1, 1),
		int2(0, -1), int2(1, -1) */ };
	float4 res = 0;
	float wei = 0;

	[unroll]
	for (int i = 0; i < 4; i++)
	{
		int2 nei = lb_id + offsets[i];

		float4 value = half_Result[nei.x][nei.y];
		float nei_d = half_Depth[nei.x][nei.y];

		float dw = (abs(nei_d - d) / d > 0.25) ? 0.001 : 1;

		float2 uv_dis = (1.0f / 6.0f) - ((nei + 0.5) / 6.0f - groupshared_uv);
		float binlinear_weight = uv_dis.x * uv_dis.y * dw;

		res += value * binlinear_weight;
		wei += binlinear_weight;
	}

	if (any(id >= uint2(_WH.xy))) return;
	res /= max(0.000000001, wei); // avoid / 0

	float2 uv = (id + 0.5) * _WH.zw;

	float3 wcpos = _V_Inv._m03_m13_m23;
	float3 p = wcpos;
	float3 pos;
	{
		float4 ndc = float4(uv * 2 - 1, 0, 1);
		float4 worldPos = mul(_VP_Inv_NoJitter, ndc);
		pos = worldPos.xyz / worldPos.w;
	}
	float3 v = normalize(pos - p);

	p.y = max(p.y, 95) + planet_radius;

	float3 sky = 0;
	float4 sc = _SceneColorTex[id];

	if (raw_d == 0) {
		sky = ScatterTable(p, v, _SunDir) * _SunLuminance;

		bool hitGround = IntersectSphere(p, v, float3(0, 0, 0), planet_radius) > 0;

		if (!hitGround) {
			sc.xyz += T_tab_fetch(p, v) * (1 - smoothstep(-0.05, 0, dot(normalize(p), _SunDir))) * Space(v) * res.a * _Brightness * 50;

			float3 coef = (numericalMieFit(dot(_SunDir, v)) + 0.25) * _Brightness;

			float4 highCloud = HighCloud(p, v);
			sc.xyz += coef * Sunlight(highCloud.yzw, _SunDir) * T(p, highCloud.yzw) * highCloud.x;

			float4 flowCloud = FlowCloud(p, v);
			sc.xyz += 2 * coef * Sunlight(flowCloud.yzw, _SunDir) * T(p, flowCloud.yzw) * flowCloud.x;
		}
	}

	_Cloud[id] = float4(res.xyz + res.a * sc.xyz, sc.a);
}


float4 _Size_XAtlas_Y_Atlas;
Texture2D<float3> _Volume2D;
RWTexture3D<float3> _Volume3D;


[numthreads(4, 4, 4)]
void LoadVolumeData(const uint3 id : SV_DispatchThreadID)
{
	uint size = _Size_XAtlas_Y_Atlas.x;
	uint2 atlas_xy = _Size_XAtlas_Y_Atlas.yz;
	uint2 id_2D = id.xy + uint2(id.z % atlas_xy.x, id.z / atlas_xy.x) * size;
	//id_2D.y = size * atlas_xy.y - 1 - id_2D.y;

	_Volume3D[id] = _Volume2D[id_2D];
}

[numthreads(8, 8, 1)]
void WriteCloudShadowMap(const uint2 id : SV_DispatchThreadID)
{
	float2 uv = (id + 0.5) / 1024;

	float3 pos = mul(_CloudMat_Inv, float4(uv, 0, 1)).xyz;

	float3 s = _SunDir;

	float max_offset = planet_radius + cloud_radi.y;

	float2 offset;
	float2 offset_ = IntersectSphere2(pos + s * max_offset, -s, float3(0, 0, 0), planet_radius + cloud_radi.y);
	offset.x = offset_.x;
	offset.y = IntersectSphere(pos + s * max_offset, -s, float3(0, 0, 0), planet_radius + cloud_radi.x);
	if (offset.y == 0) {
		offset.y = offset_.y;
	}

	if (offset.x == 0) {
		_CloudSM[id] = float4(0, 0, 0, 1);
		return;
	}

	offset -= max_offset;

	float3 wcpos = _V_Inv._m03_m13_m23;
	wcpos.y = max(wcpos.y, 95) + planet_radius;

	float l = offset.y - offset.x;

	int sample_num = _Quality.y;

	float delta_l = l / sample_num;

	float t = offset.x + delta_l * _BlueNoise[id % 1024];

	float scatter = 0;

	float2 t2 = offset.yx;

	for (int i = 0; i < sample_num; i++)
	{
		float3 sample_pos = pos - s * t;
		float cam_dis = distance(sample_pos, wcpos);
		float fade = clamp(1 - (cam_dis - 2000) / 26000, 0.2, 1);
		float lod = saturate((cam_dis - 4000) / 4000) * 6;

		float3 sha = Cloud(sample_pos, fade, lod);

		if (sha.x != 0) {
			t2.x = min(t, t2.x);
			t2.y = max(t, t2.y);
			scatter += sha.x;
		}

		t += delta_l;
	}

	scatter /= sample_num;

	_CloudSM[id] = float4(scatter, t2.x, t2.y - t2.x, scatter == 0 ? 1 : exp(-scatter * (t2.y - t2.x)));
}


groupshared float4 datas[16][33];

[numthreads(8, 8, 1)]
void BlurCloudShadowMap(int2 id : SV_DispatchThreadID, int2 groupThreadID : SV_GroupThreadID, int2 groupID : SV_GroupID)
{
	{
		uint thread_id = groupThreadID.x + groupThreadID.y * 8;
		int2 lbID = groupID * 8 + groupThreadID - 4;

		datas[groupThreadID.x][groupThreadID.y] = _CloudSM[clamp(lbID, 0, 1023)];

		int2 offset = int2(8, 0);
		datas[groupThreadID.x + offset.x][groupThreadID.y + offset.y] = _CloudSM[clamp(lbID + offset, 0, 1023)];

		offset = int2(0, 8);
		datas[groupThreadID.x + offset.x][groupThreadID.y + offset.y] = _CloudSM[clamp(lbID + offset, 0, 1023)];

		offset = int2(8, 8);
		datas[groupThreadID.x + offset.x][groupThreadID.y + offset.y] = _CloudSM[clamp(lbID + offset, 0, 1023)];

		GroupMemoryBarrierWithGroupSync();
	}

	float4 result = 0;
	float w = 0;

	int2 center = groupThreadID + 4;
	for (int i = -4; i <= 4; i++)
	{

		for (int j = -4; j <= 4; j++)
		{
			float4 s = datas[center.x + i][center.y + j];
			if (s.w != 1) {
				result += s;
				w += 1;
			}
		}
	}

	result = w == 0 ? float4(0, 0, 0, 1) : result / w;

	_CloudSM[id] = result;
}